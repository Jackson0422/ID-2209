model Festival

global {
	int numberOfPeople <- 10; 
	int numberOfStore <- 4 ; 
	int numberOfInformationCenter <- 1;
	int numberOfcop <- 1;
	
	float distanceThreshold <- 8.0;
	
	init{
		create Guest number: numberOfPeople; 
		create Store number: numberOfStore;
		create InformationCenter number: numberOfInformationCenter;
		
		Store[0].location <- {10, 10};       // 固定商店的位置信息
		Store[1].location <- {50, 80};
		Store[2].location <- {20, 70};
		Store[3].location <- {80, 20};
		InformationCenter[0].location <- {50, 50};   // 固定信息中心的位置信息
	}
}

species Guest skills: [moving]{
	// Initial hunger and thirst values, which decrease over time
	float originHungryLevel <- 150.0;
	float originThirstyLevel <- 150.0;
	float hungryLevel <- originHungryLevel;
   	float thirstyLevel <- originThirstyLevel;
   	// Record whether the guest is hungry or thirsty
   	bool isHungry <- false;
	bool isThirsty <- false;

	// Target Store Location
	point targetStoreLocation;
	// The loaction of informationCenter
	point informationCenterLocation;
	
	// Challenge 1: Memory records the stores visited recently and the types of items in the stores  BRAIN
	list<point> recentStoreLocation <- [];          // 开始记忆最近访问的商店的位置
	list<string> recentStoreType <- [];             // 开始记忆最近访问的商店的类型
	
	/* 
	 * When the information center reports that there is no suitable store, wait in the information center for the store to be replenished.
	 * "shouldWaitForSupply" indicates whether to enter this state. 
	 */ 
	bool shouldWaitForSupply <- false;
	
	// Challenge 1: Keep the speed the same and record the time required to go to the store to refill energy.
	int traveledTime <- 0;                 // 保持速度不变，记录去商店补充能量所需的时间
	int hungryTimes <- 0;                  // 饥饿次数
	bool lastHungerStatus <- false;
	
	aspect base{
		rgb agentColor <- rgb("green");
		
		if (isHungry and isThirsty){
			agentColor <- rgb("red");             
		} else if (isHungry){
			agentColor <- rgb("yellow");
		} else if (isThirsty){
			agentColor <- rgb("blue");
		}
//		if (PersonBehaviour){
//			agentColor <- rgb("black");
//		}
		draw circle(1.5) color: agentColor;
	}
	
	/*
	 * When "lastHungerStatus" is not equal to "isHungry", it means that the hunger status has changed. 
	 * Two changes mean that after being hungry, the guest went to the store to get refreshed.
	 */
	reflex recordDifferentDistance when:lastHungerStatus != isHungry {     // 根据饥饿值记录时间
		lastHungerStatus <- isHungry;
		hungryTimes <- hungryTimes + 1;                          // 开始计算距离
		if (hungryTimes = 200) {               // 当达到 200 次时，记录当前行走时间
			write traveledTime;                // 返回所需时间
		}
	}
	
	reflex getInformationCenter {
		if (informationCenterLocation = nil) {
			ask InformationCenter {
				myself.informationCenterLocation <- location;
			}
		}
	}
	
	// Hunger and thirst levels ​​decrease over time.
	reflex decreaseStatusLevel{
      if (hungryLevel > 0) {
		int hungerDecrease <- rnd(15);
        hungryLevel <- hungryLevel - hungerDecrease;
      }
      if (hungryLevel <= 70) {
        isHungry <- true;
      }
      
      if (thirstyLevel > 0) {
		int thirstyDecrease <- rnd(15);
        thirstyLevel <- thirstyLevel - thirstyDecrease;
      }
      if (thirstyLevel <= 70) {
        isThirsty <- true;
      }
   }
   
   // Challenge 1: At each step, check the stores within the distance threshold and update the memory.  检查距离阈值内是否有商店并且更新
   reflex checkSurroundings {        // 检查周围商店小于距离阈值有多少
   		list<Store> surroundingsStore <- Store where((each distance_to location) < distanceThreshold);  //  检查阈值范围内周边的商店距离
   		if (length(surroundingsStore) = 0) {
   			return;
   		}
   		loop i from:0 to:length(surroundingsStore) - 1{      // 从周边商店列表中循环
   			list<point> sameLocation <- recentStoreLocation where ((each distance_to surroundingsStore[i].location) < distanceThreshold); // 一定距离范围内更新或修改客人的记忆信息
			ask surroundingsStore[i] {
				if (length(sameLocation) = 0) {        // 如果距离为0则将信息加入 same location用于判断是否在当前客人记忆中
					do addInformationToGuestMemory(myself);                // 如果该商店不在记忆中，添加到记忆   
				} else {
					do modifyGuestMemory(myself);              // 如果已经在记忆中，更新记忆。
				}
			}
   		}
   }
   
   reflex waitForSupply when:shouldWaitForSupply = true{
   		do interactWithInformationCenter;
   }
   
	reflex move{
		if (isHungry or isThirsty) {
			point shouldGoToRecentStore <- shouldGoToRecentStore();
			if (shouldGoToRecentStore != nil) {
//			if (false) {
				targetStoreLocation <- shouldGoToRecentStore;
				do moveToStore;
			} else {
				if (targetStoreLocation = nil) {
					do moveToInformationCenter;
				} else {
					do moveToStore;
				}
			}
		} else {
			do wander speed: 5.0;
		}
	}
	
	// Challenge 1: If the return value is not nil, it means that the store in memory should be visited and the point coordinates will be returned.
	point shouldGoToRecentStore{
		if (length(recentStoreType) = 0) {
			return nil;
		}
		list<point> potentialStoreLocation <- [];
		loop i from: 0 to: length(recentStoreType) - 1{
			if (recentStoreType[i] = 'both') {                   // 根据不同的状态记忆不同的位置
				potentialStoreLocation <- potentialStoreLocation + [recentStoreLocation[i]];
			} else if (isHungry and recentStoreType[i] = "food") {
				potentialStoreLocation <- potentialStoreLocation + [recentStoreLocation[i]];
			} else if (isThirsty and recentStoreType[i] = "drink") {
				potentialStoreLocation <- potentialStoreLocation + [recentStoreLocation[i]];
			} else {
				continue;
			}
		}
		if (length(potentialStoreLocation) = 0) {
			return nil;
		}
		return potentialStoreLocation closest_to location;
	}
	
	// Go to store.
	action moveToStore {
		if ((location distance_to targetStoreLocation) < distanceThreshold) {
			Store closestStore <- Store closest_to(self);
			ask closestStore {
				bool isSuccess <- refresh(myself);
			}
		} else {
			do goto target: targetStoreLocation speed:10.0;
			traveledTime <- traveledTime + 1;          // 开始统计行走世间  如果未到达目标商店，继续以速度 10.0 移动，并增加 traveledTime（移动时间计数）
		}
	}
	
	// Go to information center.
	action moveToInformationCenter{
		do goto target: informationCenterLocation speed:10.0;
		traveledTime <- traveledTime + 1;             // 去信息中心的时间
		if ((location distance_to informationCenterLocation) < distanceThreshold) {
			do interactWithInformationCenter;
		}
	}
   
   /*
    * Interact with the information center to get the next decision.
    * It could be to get the coordinates and go to the store; it could also be to wait until a store is restocked.
    */ 
	action interactWithInformationCenter{
//		write "The person go to the InformationCenter";
		ask InformationCenter {
			point des;
			if (myself.isHungry and myself.isThirsty) {            // 根据不同的状态分配不同的商店
				// If the person is hungry and hunger is more severe than thirst
				des <- getLocationByStatus(myself, "both"); 
			} else if (myself.isHungry){
				des <- getLocationByStatus(myself, "hunger"); 
			} else {
				des <- getLocationByStatus(myself, "thirst");
			}
			if (des = nil) {
				myself.shouldWaitForSupply <- true;
			} else {
				myself.shouldWaitForSupply <- false;
				myself.targetStoreLocation <- des;
			}
		}
	}
}

species Store{                       
	aspect base{
		rgb agentColor <- rgb("lightgray");
		if(foodSupply > 0 and drinkSupply >0) {
			agentColor <- rgb("purple");
		} else if(drinkSupply > 0) {
			agentColor <- rgb("orange");
		} else if (foodSupply > 0){
			agentColor <- rgb("darkblue");
		} else{}
		draw square(3) color: agentColor;
	}
	
	// Quantity of food and drinks
	int foodSupply <- 0;
	int drinkSupply <- 0;
	// Time required for stock
	int foodRestockTimeSlot <- 0;
	int drinkRestockTimeSlot <- 0;
	
	init {
		foodSupply <- rnd(50);
		drinkSupply <- rnd(50);
	}
	
	reflex when:foodSupply = 0 {
		foodRestockTimeSlot <- foodRestockTimeSlot + 1;
		if (foodRestockTimeSlot = 10) {              // 当食物没有了，等待补充食物
			foodSupply <- rnd(50);
			foodRestockTimeSlot <- 0;
		}
	}
	
	reflex when:drinkSupply = 0{
		drinkRestockTimeSlot <- drinkRestockTimeSlot + 1;
		if (drinkRestockTimeSlot = 10) {
			drinkSupply <- rnd(50);
			drinkRestockTimeSlot <- 0;
		}
	}
	
	bool refresh(Guest guest){
		if (foodSupply = 0 and drinkSupply = 0) {
			// There is no supply, update guest memory, prompt refresh failed.
			list<point> sameLocation <- guest.recentStoreLocation where (each distance_to location < distanceThreshold);
			if (length(sameLocation) = 0) {
				do addInformationToGuestMemory(guest);
			} else {
				do modifyGuestMemory(guest);
			}
			guest.targetStoreLocation <- nil;
			return false;                        // 如果啥都没有更新客人记忆
		}
		if (foodSupply > 0 and guest.isHungry) {
			guest.hungryLevel <- guest.originHungryLevel;
			guest.isHungry <- false;
			foodSupply <- foodSupply - 1;
		}
		if (drinkSupply > 0 and guest.isThirsty) {
			guest.thirstyLevel <- guest.originThirstyLevel;
			guest.isThirsty <- false;
			drinkSupply <- drinkSupply - 1;
		}
		string storeType <- getCurrentStoreType();
		list<point> sameLocation <- guest.recentStoreLocation where (each distance_to location < distanceThreshold);
		if (length(sameLocation) = 0) {         // 如果商店未记录在客人记忆中，调用 addInformationToGuestMemory
			do addInformationToGuestMemory(guest);
		} else {                               // 如果已记录，调用 modifyGuestMemory 更新商店类型
			do modifyGuestMemory(guest);
		}
		guest.targetStoreLocation <- nil;
		return guest.isHungry or guest.isThirsty;
	}
	
	// Challenge 1: Add current store information to guest memory.
	action addInformationToGuestMemory(Guest guest){
		string storeType <- getCurrentStoreType();          // 获取到当前商店的类型
		guest.recentStoreLocation <- guest.recentStoreLocation + [location];      // 商店位置和类型分别添加到客人的记忆列表
		guest.recentStoreType <- guest.recentStoreType + [storeType];
		if (length(guest.recentStoreLocation) > 3) {        // 忆的条目数超过 3 个，移除最旧的记录，确保记忆列表维持在固定长度
			remove from:guest.recentStoreLocation index:0;
			remove from:guest.recentStoreType index:0;
		}       // 个列表保持同步，recentStoreLocation 中的每个位置对应 recentStoreType 中的商店类型
	}
	
	// Challenge 1: Modify the current store information in the customer's memory.      记录商店类型
	action modifyGuestMemory(Guest guest){
		string storeType <- getCurrentStoreType();        // 获取到商店信息
		int ind <- -1;
		loop i from: 0 to: length(guest.recentStoreLocation) - 1{       // 最近商店位置
			if (guest.recentStoreLocation[i] distance_to location < distanceThreshold){      // 如果小于阈值旧纪录
				ind <- i;                  // 记录其索引值
				break;
			}
		}
		if (ind != -1){
			guest.recentStoreType[ind] <- storeType;          // 将商店的类型和索引值匹配起来  找到匹配的商店记录（ind != -1），更新对应位置的商店类型为当前商店的类型
		}
	}
	
	string getCurrentStoreType{                // 得到商店的类型信息
		if (foodSupply > 0 and drinkSupply > 0) {
			return "both";
		} else if (foodSupply > 0) {
			return "food";
		} else if (drinkSupply > 0) {
			return "drink";
		} else {
			return "none";
		}
	}
}

species InformationCenter{
	aspect base{
		rgb agentColor <- rgb("lightpink");
		draw square(5) color: agentColor ;
	}
	list<Store> allStores <- [];
	list<point> storeLocation <- [];
	list<Guest> badBehaviourList <- [];
	init{
		ask Store {
			Store currentStore <- self;
			point currentLocation <- location;
			ask InformationCenter {
				allStores <- allStores + [currentStore];
                storeLocation <- storeLocation + [currentLocation];
            }
    	}
	}
        
     point getLocationByStatus(Guest per, string type){         // 格根据不同的状态返回不同的位置
     	bool needBoth <- (type = "both") ? true : false;
     	bool needFood <- (type = "hunger") ? true : false;
     	list<Store> potentialStores <- [];
     	if (needBoth){
     		potentialStores <- allStores where (each.foodSupply > 0 and each.drinkSupply > 0);
     		if (length(potentialStores) = 0) {
//     			write "No store have both food and drink";
				// If there are no stores that provide both food and drinks, give priority to stores that provide food.
     			needBoth <- false;
     			needFood <- true;
     		}
     	}
     	if (needFood) {
     		potentialStores <- allStores where (each.foodSupply > 0);
     		if (length(potentialStores) = 0) {
//     			write "No store have food";
     			return nil;
     		}
		} else {
			potentialStores <- allStores where (each.drinkSupply > 0);
			if (length(potentialStores) = 0) {
//     			write "No store have drink";
     			return nil;
     		}
		}
		Store closestStore <- potentialStores closest_to(per.location);
   	 	return closestStore.location;
   	 }
}

experiment festivalSimulation type: gui{
	output{
		display festivalDisplay{
			species Guest aspect:base;
			species Store aspect:base;
			species InformationCenter aspect: base;
		}
	}
}
